
zasm = { SOI ~ stmts ~ EOI }

stmts = { (stmt | NEWLINE)* }
stmt = { include
       | define
       | macro_rule
       | label
       | inst
       | ("(" ~  lit ~ ")")
       | str
       }

include = { "@include" ~ str ~ eol }
define = { "@define" ~ ident ~ lit ~ eol }
macro_rule = { "@macro" ~ ident ~ macro_args ~ macro_body }
    macro_args = { macro_arg ~ ("," ~ macro_arg)* }
    macro_arg = ${ ("%" | "#") ~ ident }
    macro_body = { "{" ~ (stmt | NEWLINE)* ~ "}" }

label = ${ lbl_name ~ ":" }

inst = { ident ~ arg_list? ~ eol}
    arg_list = { arg ~ ("," ~ arg)* }

arg = { arg_reg_imm | arg_reg | arg_imm }
    arg_reg_imm = { arg_reg ~ "(" ~ arg_imm ~ ")" }
    arg_reg     = { reg | macro_arg }
    arg_imm     = { lit | macro_arg }

lit = { expr }

lbl_name = @{ "."? ~ ident }

reg = @{ "$" ~ ident }
num = @{ ("0b" | "0o" | "0d" | "0x")? ~ "-"? ~ number }
chr = @{ "'" ~ "\\"? ~ ANY ~ "'"}
str = @{ "\"" ~ (("\\" ~ ANY) | (!"\"" ~ ANY))* ~ "\"" }

ident = @{ ident_letter ~ (ident_letter | ASCII_DIGIT)* }

expr = { atom ~ (op ~ atom)* }
    atom = { num | chr | paren | lbl_name | macro_arg }
    paren = { "(" ~ expr ~ ")" }
    op = ${ "+" | "-" | "*" | "/" | "&" | "|" | ">>" | "<<" }

number = @{ ASCII_HEX_DIGIT+ }


ident_letter = _{ ASCII_ALPHA | "_" }
eol = _{ NEWLINE | EOI }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
